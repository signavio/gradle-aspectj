plugins {
    id "com.jfrog.artifactory" version "4.20.0"
}

apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'maven'
apply plugin: 'java-gradle-plugin'

group = 'net.uberfoo.gradle'
version = '2.6-sig1-SNAPSHOT'

repositories {
    jcenter()
}

dependencies {
    implementation localGroovy()
    implementation gradleApi()
    testImplementation gradleTestKit()
    testImplementation 'junit:junit:4.12'
}

task javadocJar(type: Jar) {
    archiveClassifier = "javadoc"
    from javadoc
    from groovydoc
}

task sourcesJar(type: Jar) {
    archiveClassifier = "sources"
    from sourceSets.main.allSource
}

artifacts {
    //archives jar
    archives sourcesJar
    archives javadocJar
}

wrapper {
    gradleVersion = '6.0'
    distributionType = 'ALL'
}

/* Publishing to Signavio's artifactory */

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact javadocJar
            artifact sourcesJar

            groupId 'com.signavio'
            artifactId "gradle-aspectj-plugin"
        }
    }
}

def publishRepoKey = isChangingVersion(project.version) ? 'plugins-snapshot-local' : 'plugins-release-local'
artifactory {
    publish {
        repository {
            repoKey = publishRepoKey
        }
    }
}

static boolean isChangingVersion(String ver) {
    ver.endsWith('-SNAPSHOT') || ver.endsWith('-next')
}